plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.gradleup.shadow' version '8.3.0'
}

def mainClassName = "com.oroarmor.discordbot.DiscordBot"



run {
    def f = project.file(".env")
    if (f.exists()) {
        f.readLines().forEach {
            if (!it.startsWith("#") && !it.isEmpty()) {
                def split = it.split("\\s*=\\s*", 2)
                if (split.length == 2) {
                    environment(split[0], split[1])
                } else {
                    environment(split[0], true)
                }
            }
        }
    }

    mainClass.set(mainClassName)
}

shadowJar {
    setMainClassName(mainClassName)
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean

dependencies {
    shadow(implementation('net.dv8tion:JDA:6.1.1') {
        exclude module: 'opus-java'
    })
    shadow(implementation('com.fasterxml.jackson.core:jackson-databind:2.12.3'))
    shadow(implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.12.3'))
}

license {
    header = file("LICENSE")
}

repositories {
    mavenCentral()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}
